OLIGO = ['HI.3202.006.Index_13.BM_shift90-NAB2-PrA-CSL4pH-URA-37C-m1',
         'HI.3202.006.Index_14.BM_shift90-NAB2-PrA-CSL4pH-URA-37C-m2',
         'HI.3202.006.Index_15.BM_shift90-NAB2-PrA-CSL4pH-URA-37C-m3',
         'HI.3202.006.Index_2.BM_Ctrl-NAB2-PrA-CSL4-URA-m1',
         'HI.3202.006.Index_4.BM_Ctrl-NAB2-PrA-CSL4-URA-m2',
         'HI.3202.006.Index_5.BM_Ctrl-NAB2-PrA-CSL4-URA-m3',
         'HI.3758.003.Index_20.BM-MO_mR-enp1-37C-90-4-1',
         'HI.3758.003.Index_21.BM-MO_mR-enp1-37C-90-4-2',
         'HI.3758.004.Index_12.BM-MO_mR-srm1-37C-90-8-1',
         'HI.3758.004.Index_19.BM-MO_mR-srm1-37C-90-8-2']

rule all:
    input:
        'outputs/quant_multiqc/multiqc_data/multiqc_salmon.txt',
        "outputs/figures/perc_map_oligo.pdf", 
        "outputs/figures/perc_map_oligo_zoom.pdf",
        "outputs/figures/snoRNA_heatmap.png",
        "outputs/figures/snoRNA_profile_summary.pdf",
        "outputs/figures/its1_profile.png",
        "outputs/figures/its1_heatmap.png",
        "outputs/figures/rna_classes.pdf",
        expand('outputs/ribo/{oligo}-ribo-R1.fq.gz', oligo = OLIGO)


##################################
## GENERATE TRANSCRIPT REFERENCE
##################################

# To characterize the expression of pervasive transcripts, 
# we need to use a transcriptome that contains these transcripts.
# We can generate this reference by extracting genomic segments
# that are annotated in SGD as pervasive transcripts. These 
# annotations were compiled by Ellision et al (DOI: 10.1534/genetics.119.302262)
# We download these annotations as bed files below, and then
# use these bedfiles to extract pervasive transcripts and ORFs
# from the genome.
# The original annotations are available at 
# https://github.com/mae92/Paf1C-Transcriptome-Analysis-Code
# however, because github is non-archival, we have transfered
# these files to OSF, and download them from there. 

# We use sacCer3 from UCSC, originally obtained from the 
# following ftp link:
#
# In this folder structure, the genome is saved as "genome.fa".
# We have renamed it to sacCer3.fa to reflect its contents.
# We have also uploaded this to OSF, and use that link for
# downloading the file below. 

rule download_transcript_bedfile:
    output: 'inputs/pervasive_transcripts/all_transcripts.bed'
    shell:"wget -O {output} https://osf.io/ynh6z/download"

rule download_sacCer3:
    output: "inputs/genome/sacCer3.fa"
    shell:"wget -O {output} https://osf.io/r5cj6/download"

rule create_transcripts:
    output: "inputs/genome/sacCer3_transcripts.fa"
    input: 
        bed = 'inputs/pervasive_transcripts/all_transcripts.bed',
        genome = "inputs/genome/sacCer3.fa"
    conda: "bedtools.yml"
    shell:'''
    bedtools getfasta -fi {input.genome} -bed {input.bed} -name > {output}  
    '''

##################################
## PREPROCESSING
##################################

rule trim:
    output:
        R1 = "outputs/oligo-trim/{oligo}_R1.fastq.gz", 
        R2 = "outputs/oligo-trim/{oligo}_R2.fastq.gz",
        O1 = "outputs/oligo-trim/{oligo}_O1.fastq.gz", 
        O2 = "outputs/oligo-trim/{oligo}_O2.fastq.gz"
    input:
        R1 = "inputs/raw/{oligo}_R1.fastq.gz", 
        R2 = "inputs/raw/{oligo}_R2.fastq.gz",
        adapters = "inputs/adapters.fa"
    conda: "trim.yml"
    shell:'''
    trimmomatic PE {input.R1} {input.R2} \
            {output.R1} {output.O1} \
            {output.R2} {output.O2} \
            LEADING:2 TRAILING:2 SLIDINGWINDOW:4:2 \
            ILLUMINACLIP:{input.adapters}:2:30:10 MINLEN:31
    '''

rule bbduk_find_ribo:
    output:
        ribo1='outputs/ribo/{oligo}-ribo-R1.fq.gz',
        ribo2='outputs/ribo/{oligo}-ribo-R2.fq.gz',
    input: 
        R1 = "outputs/oligo-trim/{oligo}_R1.fastq.gz", 
        R2 = "outputs/oligo-trim/{oligo}_R2.fastq.gz",
        ribo='inputs/ribokmers.fa.gz'
    conda: 'bbduk.yml'
    shell:'''
    bbduk.sh -Xmx4g in={input.R1} in2={input.R2} outm={output.ribo1} outm2={output.ribo2} k=31 ref={input.ribo}
    '''

##################################
## QUANTIFICATION
##################################

rule index_transcriptome:
    output: directory('inputs/genome/sacCer3_transcripts_index')
    input: "inputs/genome/sacCer3_transcripts.fa"
    conda: 'salmon.yml'
    shell:'''
    salmon index -t {input} -i {output}
    '''

rule salmon:
    output: 'outputs/quant/{oligo}_quant/quant.sf',
    input:
        R1 = 'outputs/oligo-trim/{oligo}_R1.fastq.gz',
        R2 = 'outputs/oligo-trim/{oligo}_R2.fastq.gz',
        indx = directory('inputs/genome/sacCer3_transcripts_index')
    conda: 'salmon.yml'
    params: outdir = "outputs/quant/{oligo}_quant/"
    shell:'''
    salmon quant -i {input.indx} -l A -1 {input.R1} -2 {input.R2} -o {params.outdir} --validateMappings
    '''

rule multiqc:
    output: 'outputs/quant_multiqc/multiqc_data/multiqc_salmon.txt'
    input: expand('outputs/quant/{oligo}_quant/quant.sf', oligo = OLIGO)
    params: 
        indir = "outputs/quant/",
        outdir = "outputs/quant_multiqc"
    conda: "multiqc.yml"
    shell:''' 
    multiqc -o {params.outdir} {params.indir} -f
    '''
#####################################
## DIFFERENTIAL EXPRESSION
#####################################

rule deseq2:
    output:
        csl4 = "outputs/deseq2/res_csl4.csv",
        enp1 = "outputs/deseq2/res_enp1.csv", 
        srm1 = "outputs/deseq2/res_srm1.csv", 
    input: 
        expand("outputs/quant/{oligo}_quant/quant.sf", oligo = OLIGO),
        samples = "inputs/oligo-samples.csv"
    #conda: "diffex.yml" 
    script: "scripts/deseq2.R"

rule perc_map_oligo:
    output:
        perc_map_oligo = "outputs/figures/perc_map_oligo.pdf",
        perc_map_oligo_zoom = "outputs/figures/perc_map_oligo_zoom.pdf"
    input: 
        expand("outputs/quant/{oligo}_quant/quant.sf", oligo = OLIGO),
        samples = "inputs/oligo-samples.csv"
   #conda: "diffex.yml" 
    script: "scripts/perc_map_oligodt.R"

rule download_rna_classes:
    '''
    Original source: https://www.cell.com/cms/10.1016/j.cell.2013.07.047/attachment/4a4e7e81-778f-4644-b76f-1018cc5fad77/mmc3.xlsx
    DOI: https://doi.org/10.1016/j.cell.2013.07.047
    '''
    output: "inputs/rna_classes.csv" 
    shell:'''
    wget -O {output} https://osf.io/gqnwh/download
    '''

rule plot_rna_classes:
    output: 
        rna_plt_all = "outputs/figures/rna_classes.pdf",
    input:
        rna = "inputs/rna_classes.csv", 
        csl4 = "outputs/deseq2/res_csl4.csv",
        enp1 = "outputs/deseq2/res_enp1.csv", 
        srm1 = "outputs/deseq2/res_srm1.csv", 
    script: "scripts/rna_class_plots.R"

#####################################
## DEEPTOOLS
#####################################

rule hisat2_build:
    input: "inputs/genome/sacCer3.fa"
    output: "inputs/genome/sacCer3_hisat/sacCer3_hisat.1.ht2"
    conda: "hisat.yml"
    shell:"""
    hisat2-build {input} inputs/genome/sacCer3_hisat/sacCer3_hisat
    """

rule hisat2_align:
    input:
        indx = "inputs/genome/sacCer3_hisat/sacCer3_hisat.1.ht2",
        R1 = "outputs/oligo-trim/{oligo}_R1.fastq.gz", 
        R2 = "outputs/oligo-trim/{oligo}_R2.fastq.gz",
    output: "outputs/hisat/{oligo}.bam"
    params: 
        indx = "inputs/genome/sacCer3_hisat/sacCer3_hisat"
    conda: "hisat.yml"
    shell:"""
    hisat2 -p 4 -x {params.indx} -1 {input.R1} -2 {input.R2} | samtools view -Sb > {output}
    """

rule sort_bam:
    input: "outputs/hisat/{oligo}.bam"
    output: "outputs/hisat/{oligo}.sort.bam"
    conda: "hisat.yml"
    shell:'''
    samtools sort {input} > {output}
    '''
 
rule index_bam:
    input: "outputs/hisat/{oligo}.sort.bam"
    output: "outputs/hisat/{oligo}.sort.bam.bai"
    conda: "hisat.yml"
    shell:"""
    samtools index {input}    
    """

rule bam_coverage:
    input: 
        bam = "outputs/hisat/{oligo}.sort.bam",
        indx = "outputs/hisat/{oligo}.sort.bam.bai"
    output: "outputs/deeptools/{oligo}.bw"
    conda: "deeptools.yml"
    shell:"""
    bamCoverage -b {input.bam} -o {output} --normalizeUsing RPKM
    """

rule download_snoRNA_bed:
    output: "inputs/pervasive_transcripts/snoRNA.sorted.bed"
    shell:'''
    wget -O {output} https://osf.io/9n4fu/download
    '''

rule compute_matrix:
    input: 
        bw = expand("outputs/deeptools/{oligo}.bw", oligo = OLIGO),
        bed = 'inputs/pervasive_transcripts/snoRNA.sorted.bed'
    output: 
        matgz = "outputs/deeptools/matrix.mat.gz",
        mat = "outputs/deeptools/matrix.tab"
    conda: "deeptools.yml"
    shell:"""
    computeMatrix scale-regions -S {input.bw} -R {input.bed} --beforeRegionStartLength 200 --afterRegionStartLength 200 --skipZeros -o {output.matgz} --outFileNameMatrix {output.mat}
    """

rule plot_heatmap:
    input: "outputs/deeptools/matrix.mat.gz"
    output: "outputs/figures/snoRNA_heatmap.png"
    conda: "deeptools.yml"
    shell:"""
    plotHeatmap -m {input} --whatToShow "heatmap and colorbar" --dpi 700 --heatmapWidth 10 -out {output}
    """

rule plot_profile:
    input: "outputs/deeptools/matrix.mat.gz"
    output: "outputs/figures/snoRNA_profile.png"
    conda: "deeptools.yml"
    shell:"""
    plotProfile -m {input} --perGroup -out {output}
    """

rule plot_profile_summary:
    input: mat = "outputs/deeptools/matrix.tab"
    output: plt = "outputs/figures/snoRNA_profile_summary.pdf"
    #conda:
    script: "Scripts/snoRNA_metagene_summary.R"



rule compute_matrix_its1:
    input: 
        bw = expand("outputs/deeptools/{oligo}.bw", oligo = OLIGO),
        bed = 'inputs/pervasive_transcripts/its1.bed'
    output: 
        matgz = "outputs/deeptools_its1/matrix.mat.gz",
        mat = "outputs/deeptools_its1/matrix.tab"
    conda: "deeptools.yml"
    shell:"""
    computeMatrix scale-regions -S {input.bw} -R {input.bed} --beforeRegionStartLength 200 --afterRegionStartLength 200 --skipZeros -o {output.matgz} --outFileNameMatrix {output.mat}
    """

rule plot_heatmap_its1:
    input: "outputs/deeptools_its1/matrix.mat.gz"
    output: "outputs/figures/its1_heatmap.png"
    conda: "deeptools.yml"
    shell:"""
    plotHeatmap -m {input} --whatToShow "heatmap and colorbar" --dpi 700 --heatmapWidth 10 -out {output}
    """

rule plot_profile_its1:
    input: "outputs/deeptools_its1/matrix.mat.gz"
    output: "outputs/figures/its1_profile.png"
    conda: "deeptools.yml"
    shell:"""
    plotProfile -m {input} --perGroup -out {output} --samplesLabel csl4 csl4 csl4 ctrl ctrl ctrl enp1 enp1 srm1 srm1
    """
